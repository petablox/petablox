##################
# Taint analysis
##################

# name=taint-dlog

.include "Z.dom"
.include "M.dom"
.include "C.dom"
.include "V.dom"
.include "U.dom"
.include "F.dom"
.include "I.dom"
.include "Lbl.dom"

.bddvarorder Z0xZ1_M0_C0xC1_V0xV1_U0xU1_F0_I0_Lbl0xLbl1

### input relations ###

pt(c:C,v:V,o:C)  input # (c,v) points-to o

MU(m:M,u:U)              input    # u is a prim type variable in m
MV(m:M,v:V)              input    # v is a ref type variable in m
AssignPrim(v:U,u:U)      input    # v = u
LoadPrim(y:U,x:V,f:F)    input    # y = x.f
StorePrim(u:V,f:F,v:U)   input    # u.f = v
LoadStatPrim(y:U,f:F)    input    # y = f
StoreStatPrim(f:F,v:U)   input    # f = v

paramPrim(u:U,v:U,i:I)   input    # u is the formal parameter, v is actual arg, i is invocation stmt
returnPrim(u:U,v:U,i:I)  input    # u is the lhs at callsite, v is the return var, i is invocation stm

CM(c:C,m:M) input         # c is a context of method m
CI(c:C,i:I) input         # stmt i is the top element in the k-limited callstack c
CC(c:C,d:C) input         # there exists i s.t. c++[i] = d

SrcLabel(l:Lbl)  input
SinkLabel(l:Lbl) input

LabelRet(l:Lbl,m:M)             input
LabelArg(l:Lbl,m:M,z:Z)         input
ArgRetTransfer(m:M,z:Z)       input
ArgArgTransfer(m:M,z0:Z,z1:Z) input
ArgArgFlow(m:M,z0:Z,z1:Z)     input

MmethArg(m:M,z:Z,v:V)         input
MmethRet(m:M,z:Z,v:V)         input
MmethPrimArg(m:M,z:Z,u:U)     input
MmethPrimRet(m:M,z:Z,u:U)     input

### output relations ###

labelRef(c:C,v:V,d:C,l:Lbl) output   # (c,v) is labeled with (d,l)
labelPrim(c:C,u:U,d:C,l:Lbl) output  # (c,u) is labeled with (d,l)
flow(c:C,src:Lbl,d:C,sink:Lbl) output

### intermediate relations ###

varLabel(v:V,l:Lbl)
varLabelPrim(v:U,l:Lbl)

#transfer taint of u to v
transferRefRef(u:V,v:V)
transferPrimRef(u:U,v:V)
transferRefPrim(u:V,v:U)
transferPrimPrim(u:U,v:U)

flowRefRef(u:V,v:V)
flowPrimRef(u:U,v:V)
flowRefPrim(u:V,v:U)
flowPrimPrim(u:U,v:U)

label(o:C,d:C,l:Lbl)            # o is labeled with (d,l)
labelPrimFld(o:C,f:F,d:C,l:Lbl) # prim fld f of object o is labeled (d,l)
labelPrimFldStat(f:F,d:C,l:Lbl) # static field f of primitive type has label (d,l)

sink(c:C,u:V,d:C,l:Lbl)         # (c,u) is labeled with sink label (d,l)
sinkPrim(c:C,u:U,d:C,l:Lbl)     # (c,u) is labeled with sink label (d,l)

###################
#          RULES
###################

# translation of annotation facts

varLabel(v,l)     :- LabelRet(l,m), MmethRet(m,_,v).
varLabel(v,l)     :- LabelArg(l,m,z), MmethArg(m,z,v).
varLabelPrim(v,l) :- LabelRet(l,m), MmethPrimRet(m,_,v).
varLabelPrim(v,l) :- LabelArg(l,m,z), MmethPrimArg(m,z,v).

transferRefRef(u,v)   :- ArgArgTransfer(m,z0,z1), MmethArg(m,z0,u), MmethArg(m,z1,v).
transferRefPrim(u,v)  :- ArgArgTransfer(m,z0,z1), MmethArg(m,z0,u), MmethPrimArg(m,z1,v).
transferPrimRef(u,v)  :- ArgArgTransfer(m,z0,z1), MmethPrimArg(m,z0,u), MmethArg(m,z1,v).
transferPrimPrim(u,v) :- ArgArgTransfer(m,z0,z1), MmethPrimArg(m,z0,u), MmethPrimArg(m,z1,v).

transferRefRef(u,v)   :- ArgRetTransfer(m,z), MmethArg(m,z,u), MmethRet(m,_,v).
transferRefPrim(u,v)  :- ArgRetTransfer(m,z), MmethArg(m,z,u), MmethPrimRet(m,_,v).
transferPrimRef(u,v)  :- ArgRetTransfer(m,z), MmethPrimArg(m,z,u), MmethRet(m,_,v).
transferPrimPrim(u,v) :- ArgRetTransfer(m,z), MmethPrimArg(m,z,u), MmethPrimRet(m,_,v).

flowRefRef(u,v)   :- ArgArgFlow(m,z0,z1), MmethArg(m,z0,u), MmethArg(m,z1,v).
flowRefPrim(u,v)  :- ArgArgFlow(m,z0,z1), MmethArg(m,z0,u), MmethPrimArg(m,z1,v).
flowPrimRef(u,v)  :- ArgArgFlow(m,z0,z1), MmethPrimArg(m,z0,u), MmethArg(m,z1,v).
flowPrimPrim(u,v) :- ArgArgFlow(m,z0,z1), MmethPrimArg(m,z0,u), MmethPrimArg(m,z1,v).

# (backward) escape flow

sink(c,u,d,l) :- CM(c,m), MV(m,u), varLabel(u,l), SinkLabel(l), c = d.
sink(c,u,d,l) :- flowRefRef(u,v), labelRef(c,v,d,l), SinkLabel(l).
sink(c,u,d,l) :- flowRefPrim(u,v), labelPrim(c,v,d,l), SinkLabel(l).

sinkPrim(c,u,d,l) :- CM(c,m), MU(m,u), varLabelPrim(u,l), SinkLabel(l), c = d.
sinkPrim(c,u,d,l) :- flowPrimRef(u,v), labelRef(c,v,d,l), SinkLabel(l).
sinkPrim(c,u,d,l) :- flowPrimPrim(u,v), labelPrim(c,v,d,l), SinkLabel(l).

# complete flow

flow(c,src,e,sink) :- sink(d,u,e,sink), labelRef(d,u,c,src), SrcLabel(src).
flow(c,src,e,sink) :- sinkPrim(d,u,e,sink), labelPrim(d,u,c,src), SrcLabel(src).

# (forward) label flow

label(o,c,l)      :- varLabel(v,l), CM(c,m), MV(m,v), pt(c,v,o).
label(o,d,l)      :- transferRefRef(u,v), labelRef(c,u,d,l), pt(c,v,o).
label(o,d,l)      :- transferPrimRef(u,v), labelPrim(c,u,d,l), pt(c,v,o).
labelRef(c,v,d,l) :- pt(c,v,o), label(o,d,l).

labelPrim(c,v,d,l) :- varLabelPrim(v,l), CM(c,m), MU(m,v), c = d.
labelPrim(c,v,d,l) :- transferRefPrim(u,v), labelRef(c,u,d,l).
labelPrim(c,v,d,l) :- transferPrimPrim(u,v), labelPrim(c,u,d,l).

labelPrim(c,v,d,l)      :- AssignPrim(v,u), labelPrim(c,u,d,l).
labelPrimFld(o,f,d,l)   :- labelPrim(c,v,d,l), StorePrim(u,f,v), pt(c,u,o).
labelPrim(c,y,d,l)      :- LoadPrim(y,x,f), labelPrimFld(o,f,d,l), pt(c,x,o).
labelPrimFldStat(f,d,l) :- StoreStatPrim(f,v), labelPrim(_,v,d,l).
labelPrim(c,y,d,l)      :- LoadStatPrim(y,f), labelPrimFldStat(f,d,l), MU(m,y), CM(c,m).

labelPrim(d,u,e,l) :- labelPrim(c,v,e,l), paramPrim(u,v,i), CC(c,d), CI(d,i).
labelPrim(c,u,e,l) :- labelPrim(d,v,e,l), returnPrim(u,v,i), CI(d,i), CC(c,d).
