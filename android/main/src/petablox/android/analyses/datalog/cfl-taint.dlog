###################
# CONFIGURATION
###################

# name=cfl-taint-dlog

.include "Z.dom"
.include "M.dom"
.include "C.dom"
.include "V.dom"
.include "U.dom"
.include "F.dom"
.include "I.dom"
.include "Lbl.dom"

.bddvarorder Z0xZ1_M0_Lbl0xLbl1_C0xC1xC2_V0xV1_U0xU1_F0_I0

###################
# INPUT: POINTER
###################

PtFull(c:C,v:V,o:C)    input
FptFull(o1:C,f:F,o2:C) input

###################
# INPUT: TAINT HELPER
###################

LoadPrimCtxt(c:C,v:U,u:V,f:F) input
LoadStatPrimCtxt(c:C,v:U,f:F) input
StorePrimCtxt(c:C,v:V,f:F,u:U) input
StoreStatPrimCtxt(c:C,f:F,v:U) input

AssignPrimCtxt(c:C,v:U,u:U) input
AssignPrimArgCCtxt(c:C,v:U,d:C,u:U) input
AssignPrimRetCCtxt(c:C,v:U,d:C,u:U) input
AssignPrimCCtxt(c:C,v:U,d:C,u:U) input

###################
# INPUT: ANNOTATIONS
###################

# Src label
Src2Label(cl:Lbl) input
Sink2Label(cl:Lbl) input

# Label
Label2RefT(c:C,cl:Lbl,v:V) input
Label2PrimT(c:C,cl:Lbl,v:U) input

# Sink full label (out labels)
SinkF2RefF(c:C,cl:Lbl,v:V) input
SinkF2PrimF(c:C,cl:Lbl,v:U) input

# Transfer
Ref2RefTFull(c:C,v:V,u:V) input
Ref2PrimTFull(c:C,v:V,u:U) input
Prim2RefTFull(c:C,v:U,u:V) input
Prim2PrimTFull(c:C,v:U,u:U) input

# Flow (!)
Ref2RefF(c:C,v:V,u:V) input
Ref2PrimF(c:C,v:V,u:U) input
Prim2RefF(c:C,v:U,u:V) input
Prim2PrimF(c:C,v:U,u:U) input

###################
# INTERMEDIATE: OBJECT ANNOTATIONS
###################

Obj2RefT(c:C,o:C,v:V) output
Obj2PrimT(c:C,o:C,v:U) output

Label2ObjT(cl:Lbl,o:C) output

###################
# INTERMEDIATE: SINKF
###################

# Sink_full-obj flow
SinkF2Obj(cl:Lbl,o:C) output

# Sink_full-prim flow
SinkF2Prim(c:C,cl:Lbl,v:U) output

###################
# INTERMEDIATE: SRC-SINK FLOW
###################

Src2Sink(src:Lbl,sink:Lbl) output

###################
# INTERMEDIATE: LABEL FLOW
###################

# Label-obj flow

Label2Obj(cl:Lbl,o:C) output
Label2ObjX(cl:Lbl,o2:C) output

# Label-prim flow

Label2Prim(c:C,cl:Lbl,v:U) output
Label2PrimFld(cl:Lbl,o:C,f:F) output
Label2PrimFldStat(cl:Lbl,f:F) output

###################
# OUTPUT: RESULTS
###################

labelRefTaint(c:C,v:V,l:Lbl)  output # (c,v) is labeled with l
labelPrimTaint(c:C,v:U,l:Lbl) output # (c,v) is labeled with l
flowTaint(src:Lbl,sink:Lbl) output

###################
# RULES: OBJECT ANNOTATIONS
###################

Obj2RefT(c,o,u)   :- PtFull(c,v,o), Ref2RefTFull(c,v,u). # pt is backwards
Obj2PrimT(c,o,u)  :- PtFull(c,v,o), Ref2PrimTFull(c,v,u). # pt is backwards
Obj2RefT(c,o1,v)  :- FptFull(o2,0,o1), Obj2RefT(c,o2,v). # fpt is backwards
Obj2PrimT(c,o1,v) :- FptFull(o2,0,o1), Obj2PrimT(c,o2,v). # fpt is backwards

Label2ObjT(cl,o)  :- Label2RefT(c,cl,v), PtFull(c,v,o).
Label2ObjT(cl,o2) :- Label2ObjT(cl,o1), FptFull(o1,_,o2).

###################
# RULES: SINKF
###################

# Sink_full-obj flow

SinkF2Obj(cl,o)  :- SinkF2RefF(c,cl,v), PtFull(c,v,o).
SinkF2Obj(cl,o2) :- Sink2Label(cl), Label2Obj(cl,o1), PtFull(c,v,o1), Ref2RefF(c,u,v), PtFull(c,u,o2). # first pt, Ref2RefF are backwards
SinkF2Obj(cl,o)  :- Sink2Label(cl), Label2Prim(c,cl,v), Ref2PrimF(c,u,v), PtFull(c,u,o). # Ref2PrimF is backwards
SinkF2Obj(cl,o2) :- SinkF2Obj(cl,o1), FptFull(o1,_,o2).

# Sink_full-prim flow

SinkF2Prim(c,cl,v) :- SinkF2PrimF(c,cl,v).
SinkF2Prim(c,cl,u) :- Sink2Label(cl), Label2Obj(cl,o), PtFull(c,v,o), Prim2RefF(c,u,v). # pt, Prim2RefF are backwards
SinkF2Prim(c,cl,u) :- Sink2Label(cl), Label2Prim(c,cl,v), Prim2PrimF(c,u,v). # Prim2PrimF is backwards

###################
# RULES: SRC-SINK FLOW
###################

Src2Sink(src,sink) :- Src2Label(src), Label2Obj(src,o), SinkF2Obj(sink,o). # SinkF2Obj is backwards
Src2Sink(src,sink) :- Src2Label(src), Label2Prim(c,src,v), SinkF2Prim(c,sink,v). # SinkF2Prim is backwards
Src2Sink(src,sink) :- Src2Label(src), Label2PrimFld(src,o,_), SinkF2Obj(sink,o). # SinkF2Obj is backwards

###################
# RULES: LABEL FLOW
###################

# Label-obj flow

Label2Obj(cl,o)   :- Label2ObjT(cl,o).
Label2Obj(cl,o)   :- Label2ObjX(cl,o).

Label2ObjX(cl,o2) :- Label2Obj(cl,o1), Obj2RefT(c,o1,v), PtFull(c,v,o2).
Label2ObjX(cl,o)  :- Label2Prim(c,cl,v), Prim2RefTFull(c,v,u), PtFull(c,u,o).
Label2ObjX(cl,o2) :- Label2PrimFld(cl,o1,0), Obj2RefT(c,o1,v), PtFull(c,v,o2).
Label2ObjX(cl,o2) :- Label2ObjX(cl,o1), FptFull(o1,0,o2).

# Label-prim flow

Label2Prim(c,cl,v) :- Label2PrimT(c,cl,v).
Label2Prim(c,cl,u) :- Label2Prim(c,cl,v), AssignPrimCtxt(c,u,v). # AssignPrim is flipped
Label2Prim(d,cl,u) :- Label2Prim(c,cl,v), AssignPrimCCtxt(d,u,c,v). # AssignPrimCtxt is flipped

Label2Prim(c,cl,v) :- Label2Obj(cl,o), Obj2PrimT(c,o,v).
Label2Prim(c,cl,u) :- Label2Prim(c,cl,v), Prim2PrimTFull(c,v,u).

Label2Prim(c,cl,u) :- Label2ObjT(cl,o), PtFull(c,v,o), LoadPrimCtxt(c,u,v,_). # pt is backwards, LoadPrimCtxt is flipped
Label2Prim(c,cl,u) :- Label2ObjX(cl,o), PtFull(c,v,o), LoadPrimCtxt(c,u,v,0). # pt is backwards, LoadPrimCtxt is flipped
Label2Prim(c,cl,v) :- Label2PrimFld(cl,o,0), Obj2PrimT(c,o,v).

Label2Prim(c,cl,u) :- Label2PrimFld(cl,o,f), PtFull(c,v,o), LoadPrimCtxt(c,u,v,f). # pt is backwards, LoadPrim is flipped
Label2Prim(c,cl,u) :- Label2PrimFldStat(cl,f), LoadStatPrimCtxt(c,u,f). # LoadStatPrimCtxt is flipped

# Label-prim_fld flow

Label2PrimFld(cl,o,f)   :- Label2Prim(c,cl,v), StorePrimCtxt(c,u,f,v), PtFull(c,u,o). # StorePrimCtxt is flipped
Label2PrimFldStat(cl,f) :- Label2Prim(c,cl,v), StoreStatPrimCtxt(c,f,v). # StoreStatPrimCtxt is flipped

###################
# RULES: OUTPUT
###################

labelRefTaint(c,v,l)  :- Label2Obj(l,o), PtFull(c,v,o).
labelPrimTaint(c,u,l) :- Src2Label(l), Label2Prim(c,l,u).
flowTaint(src,sink)   :- Src2Sink(src,sink).
