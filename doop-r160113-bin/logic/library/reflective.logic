/*************************************************************
 * Implementation of points-to propagation for concrete,
 * non-reflective, operations that have been discovered using
 * reflection.
 *
 * @author Martin Bravenboer
 *************************************************************/


/*************************************************************
 * Reflective method calls
 *
 * Issues in the documentation:
 *   - Class.getDeclaredMethods does not specify that <init> is not returned.
 *   - Method.invoke does not specify that private method calls do not do method lookup
 *   - It is impossible to call an overridden method in a super class.
 *
 * Issues in Livshits' reflection analysis:
 *   - no dynamic method lookup for instance methods (interface, abstract, overriden)
 *   - no handling of exceptions
 *   - unclear how to make context-sensitive
 *   - not sound for reflective use of reflection
 *   - no handling of boxing/unboxing
 *
 * Test:
 *   - static initialization
 *   - context-sensivitity
 *   - virtual method lookup
 *   - method declared in class of objParam
 *
 * TODO Boxing of primitive results
 *
 *************************************************************/

/**
 * Method invocations resulting from propagating Method and
 * Constructor objects to reflective method invocations.
 *
 * As opposed to method invocations in the input facts, these must be
 * context-sensitive for precision purposes: for a reflective method
 * call sites, the invoked method can depend on the context.
 */
ReflectiveMethodInvocation(?ctx, ?caller, ?signature) ->
  Context(?ctx), MethodInvocationRef(?caller),
  MethodSignatureRef(?signature).

/**
 * The base variable of a reflective method call.
 */
ReflectiveBaseVar[?caller] = ?base ->
  MethodInvocationRef(?caller),
  VarRef(?base).

/**
 * The return variable of a reflective method call.
 */
ReflectiveAssignReturnValue[?caller] = ?base ->
  MethodInvocationRef(?caller),
  VarRef(?base).

/**
 * The variable that points-to an array of actual parameters of a
 * reflective method call.
 */
ReflectiveActualParams[?caller] = ?actualParams ->
  MethodInvocationRef(?caller),
  VarRef(?actualParams).

/*************************************************************
 * Specific reflective method invocations
 * 
 * Reflective method invocations need to be specialized to determine
 * what kind of method call this is.
 *
 *************************************************************/

ReflectiveSpecialMethodInvocation(?ctx, ?caller, ?signature) ->
  Context(?ctx), MethodInvocationRef(?caller),
  MethodSignatureRef(?signature).

ReflectiveStaticMethodInvocation(?ctx, ?caller, ?signature) ->
  Context(?ctx), MethodInvocationRef(?caller),
  MethodSignatureRef(?signature).

/* YANNIS: reordered */
ReflectiveVirtualMethodInvocation(?signature, ?ctx, ?caller) ->
  Context(?ctx), MethodInvocationRef(?caller),
  MethodSignatureRef(?signature).

/**
 * Private method invocations are special methods, unless they are static.
 *
 * Note that (for now) the other special method invocation cases are
 * not possible: 1) instance initialization methods cannot be invoked
 * reflectively using Method.invoke and 2) overriden superclass
 * methods cannot be invoked through reflection.
 */
ReflectiveSpecialMethodInvocation(?ctx, ?caller, ?signature) <-
  ReflectiveMethodInvocation(?ctx, ?caller, ?signature),
  ModifierRef:Value(?private:"private"),
  MethodModifier(?private, ?signature),
  ModifierRef:Value(?static:"static"),
  ! MethodModifier(?static, ?signature).

/**
 * Static method invocations
 */
ReflectiveStaticMethodInvocation(?ctx, ?caller, ?signature) <-
  ReflectiveMethodInvocation(?ctx, ?caller, ?signature),
  ModifierRef:Value(?static:"static"),
  MethodModifier(?static, ?signature).

/**
 * All others are virtual method invocations
 */
ReflectiveVirtualMethodInvocation(?signature, ?ctx, ?caller) <-
  ReflectiveMethodInvocation(?ctx, ?caller, ?signature),
  ModifierRef:Value(?private:"private"),
  ! MethodModifier(?private, ?signature),
  ModifierRef:Value(?static:"static"),
  ! MethodModifier(?static, ?signature).

/*************************************************************
 * Reflective call graph edges
 *
 * TODO Unfortunately we have to redo the implementation of method
 *      calls here. It might be a good idea to consider generation of
 *      method invocations (but those are input predicates).
 *
 *************************************************************/

/**
 * A reflective call graph edge has specific details for propagating
 * parameters and handling return values, so we distinguish them from
 * native and normal call graph edges.
 */
ReflectiveCallGraphEdge(?callerCtx, ?caller, ?calleeCtx, ?callee) ->
  Context(?callerCtx), MethodInvocationRef(?caller),
  Context(?calleeCtx), MethodSignatureRef(?callee).

ReachableContext(?calleeCtx, ?callee) <-
  ReflectiveCallGraphEdge(_, _, ?calleeCtx, ?callee).

/**
 * Static methods
 *
 * TODO Macro for context
 */
ReflectiveCallGraphEdge(?callerCtx, ?caller, ?callerCtx, ?callee) <-
  ReflectiveStaticMethodInvocation(?callerCtx, ?caller, ?callee).

/**
 * Special methods
 *
 * TODO Check if object is an instance of the class declaring the
 *      method (only overapproximation)
 *
 * TODO Macro for context
 */
ReflectiveCallGraphEdge(?callerCtx, ?caller, ?callerCtx, ?callee),
VarPointsTo(?hctx, ?heap, ?callerCtx, ?this)
  <-
  ReflectiveBaseVar[?caller] = ?base, // TODO make ?caller specific
  VarPointsTo(?hctx, ?heap,  ?callerCtx, ?base), // recursive
  ReflectiveSpecialMethodInvocation(?callerCtx, ?caller, ?callee),  // recursive // TODO switch order
  ThisVar[?callee] = ?this,
  AssignCompatible(Var:Type[?this], HeapAllocation:Type[?heap]).

/**
 * Virtual methods
 *
 * TODO Check if object is an instance of the class declaring the
 *      method (only overapproximation)
 *
 * TODO Macro for context
 */
ReflectiveCallGraphEdge(?callerCtx, ?caller, ?callerCtx, ?tomethod),
VarPointsTo(?hctx, ?heap, ?callerCtx, ?this)
  <-
  ReflectiveBaseVar[?caller] = ?base,
  VarPointsTo(?hctx, ?heap, ?callerCtx, ?base), // recursive
  ReflectiveVirtualMethodInvocation(?signature, ?callerCtx, ?caller),  // recursive
  HeapAllocation:Type[?heap] = ?heaptype,
  MethodSignature:Type[?signature] = ?type,
  AssignCompatible(?type, ?heaptype), // TODO need nicer check
  MethodSignature:SimpleName[?signature] = ?simplename,
  MethodSignature:Descriptor[?signature] = ?descriptor,
  MethodLookup[?simplename, ?descriptor, ?heaptype] = ?tomethod,
  ThisVar[?tomethod] = ?this.

/**
 * Return values of reflective method invocations
 */
Assign(?type, ?callerCtx, ?local, ?calleeCtx, ?return) <-
  ReturnVar(?return, ?callee),
  ReflectiveCallGraphEdge(?callerCtx, ?invocation, ?calleeCtx, ?callee),
  ReflectiveAssignReturnValue[?invocation] = ?local,
  Var:Type[?local] = ?type.

ReflectiveLoadArrayIndex(?calleeCtx, ?formal, ?callerCtx, ?actual) <-
  ReflectiveCallGraphEdge(?callerCtx, ?invocation, ?calleeCtx, ?callee),
  FormalParam[_, ?callee] = ?formal,
  ReflectiveActualParams[?invocation] = ?actual.

/*************************************************************
 *
 * Reflective heap allocations
 *
 *************************************************************/

ReflectiveAssignHeapAllocation(?instruction, ?type, ?ctx, ?var) ->
  Context(?ctx), VarRef(?var), Type(?type), InstructionRef(?instruction).

InitializedClass(?type),
RecordMacro(?ctx, ?heap, ?hctx),
VarPointsTo(?hctx, ?heap, ?ctx, ?var) <-
  ReflectiveAssignHeapAllocation(?instruction, ?type, ?ctx, ?var), // recursive
  ReflectiveHeapAllocation[?instruction, ?type]  = ?heap,
  ObjectShouldNotBeRefined(?heap).

#ifdef RecordRefinedMacro
InitializedClass(?type),
RecordRefinedMacro(?ctx, ?heap, ?hctx),
VarPointsTo(?hctx, ?heap, ?ctx, ?var) <-
  ReflectiveAssignHeapAllocation(?instruction, ?type, ?ctx, ?var), // recursive
  ReflectiveHeapAllocation[?instruction, ?type]  = ?heap,
  ObjectShouldBeRefined(?heap).
#endif

/*************************************************************
 *
 * Reflective assignment of class constants
 *
 *************************************************************/

ReflectiveAssignClassConstant(?ctx, ?invocation, ?type) ->
  Context(?ctx), MethodInvocationRef(?invocation), Type(?type).

/**
 * Not every virtual method invocation has a return value, so we need
 * to initialize classes separate from the VarPointsTo rule.
 */
InitializedClass(?type) <-
  ReflectiveAssignClassConstant(_, _, ?type).

/**
 * TODO This doesn't make any sense without a 'to' variable.
 */
RecordImmutableMacro(?ctx, ?heap, ?immCtx),
VarPointsTo(?immCtx, ?heap, ?ctx, ?return) <-
  ReflectiveAssignClassConstant(?ctx, ?invocation, ?type),
  ReifiedClass[?type] = ?heap,
  AssignReturnValue[?invocation] = ?return.

/*************************************************************
 *
 * Reflective load of array index.
 *
 *************************************************************/

ReflectiveLoadArrayIndex(?calleeCtx, ?to, ?callerCtx, ?base) ->
  Context(?calleeCtx), VarRef(?to),
  Context(?callerCtx), VarRef(?base).

/**
 * TODO it's unfortunate this code is so similar to normal LoadArrayIndex.
 */
LoadHeapArrayIndex(?calleeCtx, ?to, ?basehctx, ?baseheap) <-
  ReflectiveLoadArrayIndex(?calleeCtx, ?to, ?callerCtx, ?base),
  VarPointsTo(?basehctx, ?baseheap, ?callerCtx, ?base).


/*************************************************************
 *
 * Reflective load of a static field
 *
 * TODO does this trigger class initialization?
 *
 *************************************************************/

ReflectiveLoadStaticField(?ctx, ?var, ?signature) ->
  FieldSignatureRef(?signature), Context(?ctx), VarRef(?var).

VarPointsTo(?hctx, ?heap, ?ctx, ?var) <-
  ReflectiveLoadStaticField(?ctx, ?var, ?signature),
  StaticFieldPointsTo(?hctx, ?heap, ?signature).

/*************************************************************
 *
 * Reflective store of a static field
 *
 * TODO does this trigger class initialization?
 *
 *************************************************************/

ReflectiveStoreStaticField(?signature, ?ctx, ?var) ->
  FieldSignatureRef(?signature), Context(?ctx), VarRef(?var).

StaticFieldPointsTo(?hctx, ?heap, ?signature) <-
  ReflectiveStoreStaticField(?signature, ?ctx, ?var),
  VarPointsTo(?hctx, ?heap, ?ctx, ?var).

/*************************************************************
 *
 * Reflective load of an instance field
 *
 *************************************************************/

ReflectiveLoadInstanceField(?toCtx, ?to, ?signature, ?baseCtx, ?base) ->
  Context(?baseCtx), VarRef(?base), FieldSignatureRef(?signature),
  Context(?toCtx), VarRef(?to).

LoadHeapInstanceField(?toCtx, ?to, ?sig, ?basehctx, ?baseheap) <-
  ReflectiveLoadInstanceField(?toCtx, ?to, ?sig, ?baseCtx, ?base),
  VarPointsTo(?basehctx, ?baseheap, ?baseCtx, ?base).

/*************************************************************
 *
 * Reflective store of an instance field
 *
 *************************************************************/

ReflectiveStoreInstanceField(?fromCtx, ?from, ?signature, ?baseCtx, ?base) ->
  Context(?fromCtx), VarRef(?from), Context(?baseCtx), VarRef(?base),
  FieldSignatureRef(?signature).

StoreHeapInstanceField(?signature, ?basehctx, ?baseheap, ?fromCtx, ?from) <-
  ReflectiveStoreInstanceField(?fromCtx, ?from, ?signature, ?baseCtx, ?base),
  VarPointsTo(?basehctx, ?baseheap, ?baseCtx, ?base).
